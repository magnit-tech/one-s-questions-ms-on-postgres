#Область ПрограммныйИнтерфейс

// Режим = 1 - только текст запрса, 2 - с текстом создания необходимых ВТ, 3 - кроме создания еще и с заполнением создаваемых ВТ - ПОКА НЕ СДЕЛАЛ
Процедура СкопироватьВБуферТекстЗапроса(НастройкаТеста, ТекущиеДанные, Режим = 2) Экспорт 
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекстЗапроса = СформироватьТекстЗапроса(НастройкаТеста, ТекущиеДанные, Режим);
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ПоместитьВБуфераОбмена(ТекстЗапроса);	
		КонецЕсли;                          
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СкопироватьВБуферСсылкуНаСобытиеВOpensearch(НастройкаТеста, ТекущиеДанные) Экспорт 
	
	Если ТекущиеДанные <> Неопределено Тогда 

		НомерСобытия = ТекущиеДанные.НомерСобытия;
		СсылкаНаСобытие = НастройкаТестовКлиентСервер.СсылкаНаСобытиеВOpensearch(НомерСобытия, НастройкаТеста);
		
		Если ЗначениеЗаполнено(СсылкаНаСобытие) Тогда
			ПоместитьВБуфераОбмена(СсылкаНаСобытие);	
		КонецЕсли;                          
		
	КонецЕсли;
	
КонецПроцедуры
 

Функция СформироватьТекстЗапроса(НастройкаТеста, ТекущиеДанные, Режим = 2) Экспорт 
	
	Результат = НастройкаТестовВызовСервера.СформироватьТекстЗапроса(НастройкаТеста,ТекущиеДанные.ДатаСобытия, ТекущиеДанные.НомерСессии,ТекущиеДанные.НомерСобытия,Режим);
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции 

Асинх Функция ПоместитьВБуфераОбмена(Значение, ФорматДанных = Неопределено)
	
	Если ФорматДанных = Неопределено Тогда
		ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Текст;	
	КонецЕсли;                                                   
	
    Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		
		Если Ждать СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(ФорматДанных) Тогда
            ПомещаемыеДанные = Новый ЭлементБуфераОбмена(ФорматДанных, Значение);
            Возврат Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ПомещаемыеДанные);
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Использование формата данных %1 в буфере обмена недопустимо!", Строка(ФорматДанных)));	
		КонецЕсли;
		
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Использование буфера обмена недоступно!");	
		
    КонецЕсли;
	
    Возврат Ложь;
		
КонецФункции 

#КонецОбласти